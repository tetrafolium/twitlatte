/*
 * Copyright 2015-2019 The twitlatte authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28

        applicationId 'com.github.moko256.twitlatte'
        versionCode 30
        versionName '1.10.1'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'

        Base64.Decoder e = Base64.decoder

        buildConfigField 'byte[]', 'p', "{${e.decode(e.decode(d+a+c+b+"=")).join(",")}}"
        vectorDrawables.useSupportLibrary = true
        resConfigs 'en', 'ja', 'fr', 'ldltr'
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-release.txt'
        }
        debug {
            debuggable true
            shrinkResources true
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.txt'
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    packagingOptions {
        exclude '/lib/**'
        exclude '/javax/**'
        exclude '/okhttp3/**'
        exclude '/kotlin/**'
        exclude '/META-INF/*.version'
        exclude '/META-INF/**.kotlin_module'
        exclude '/META-INF/proguard/**'
    }
}

dependencies {
    // Include
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation project(':component_client_twitter')
    implementation project(':component_client_mastodon')

    implementation project(':japng-android:android_api')

    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-rc01'
    implementation 'androidx.browser:browser:1.0.0'

    //noinspection GradleDependency
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-rc01'

    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoplayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayerVersion"
    implementation "com.google.android.exoplayer:extension-okhttp:$exoplayerVersion"

    implementation project(':alternative_glide')
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"

    implementation 'jp.wasabeef:glide-transformations:4.0.1'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.chuross:flinglayout:0.5.0'

    // Local test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation 'androidx.test.ext:junit:1.1.1'

    // Instrument test
    androidTestUtil 'androidx.test:orchestrator:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
//  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

repositories {
    jcenter {
        content {
            includeGroup 'com.google.android.exoplayer'
            includeModule('jp.wasabeef', 'glide-transformations')
            includeModule('org.jetbrains.trove4j', 'trove4j')
        }
    }
    maven {
        url 'https://jitpack.io'
        content {
            includeModule('com.github.chrisbanes', 'PhotoView')
            includeModule('com.github.chuross', 'flinglayout')
        }
    }
    google()
    mavenCentral()
}

buildscript {
    ext.a = 'RmxhbXRVTVdKdWRWQjRlbk40UlVoSk9F'
    ext.b = 'Tk2Y0U0MWExb3dhMjAwZEVGMlRrbz0'
    ext.c = 'cGliR0Z5UkRsbGNXWk1PVkJRYjFaUlZGTlZRb'
    ext.d = 'VkRsNWJtTTFUblJQY3pOS1ZHWlZZVEJXWjNodGJE'
    ext.e = 'p2ZW5CT05XdGFNlaFlTeffmw1xYm1N'
}
